// VCampus项目配置
plugins {
    id 'idea'
    id 'eclipse'
}


allprojects {
    group = 'com.vcampus'
    version = '1.0.0'
    
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    
    dependencies {
        // 基本日志
        implementation 'org.slf4j:slf4j-simple:2.0.7'
        
        // Lombok - 简化代码注解 (升级到支持JDK 21的版本)
        compileOnly 'org.projectlombok:lombok:1.18.32'
        annotationProcessor 'org.projectlombok:lombok:1.18.32'
        
        // 测试
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
        testCompileOnly 'org.projectlombok:lombok:1.18.32'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
    }
    
    test {
        useJUnitPlatform()
    }
    
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.annotationProcessorPath = configurations.annotationProcessor
        
        // 禁用编译警告，避免中文乱码
        options.compilerArgs << '-Xlint:none'
        options.compilerArgs << '-nowarn'
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true

            // 确保正确识别源码目录
            sourceDirs += file('src/main/java')
            testSourceDirs += file('src/test/java')
            resourceDirs += file('src/main/resources')
            testResourceDirs += file('src/test/resources')

            // 排除构建目录
            excludeDirs += file('build')
            excludeDirs += file('.gradle')
        }
    }
}